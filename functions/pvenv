# vim:et sts=2 sw=2 ft=zsh
local -r pusage="Usage: %B${0}%b <command> [arg...]

Commands:
  %Buse%b [python] [arg...]  Creates venv for the current directory using the provided python executa-
                         ble (if venv doesn't exist) and activates it. If no python executable is
                         provided, defaults to %Bpython3%b. Any additional arguments given are passed
                         to the venv creation script. See %Bpython3 -m venv --help%b
  %Bls%b                     Lists path of venv associated with current directory, if venv exists.
  %Brm%b                     Removes venv associated with current directory."

if (( # < 1 )); then
  print -u2 -PR ${pusage}
  return 2
fi

: ${PVENV_HOME=${HOME}/.venvs}
local -r pvenv_name=$(python3 -c 'import base64
import hashlib
import os
import re
d=os.getcwd()
n=re.sub(r"[ &$`!*@\"\\\r\n\t]", "_", os.path.basename(d)[:42])
h=base64.urlsafe_b64encode(hashlib.sha1(d.encode()).digest()).decode()[:8]
print("{}-{}".format(n, h))')
local -r pvenv_dir=${PVENV_HOME}/${pvenv_name}

case ${1} in
  use)
    if [[ -e ${pvenv_dir} ]]; then
      print -R "Activating venv under ${pvenv_dir}"
      source ${pvenv_dir}/bin/activate
    else
      shift
      local ppython
      if (( # )); then
        ppython=${1}
        shift
      else
        ppython=python3
      fi
      print -PR "%BCreating venv under ${pvenv_dir}${@:+ }${@} using ${ppython} and activating it%b"
      local pfile
      ${ppython} -m venv venv ${pvenv_dir} "${@}" && source ${pvenv_dir}/bin/activate && \
          print -P "%BUpgrading pip%b" && python -m pip install --upgrade pip && \
          for pfile in *requirements*.txt setup.py; do
            if [[ -f ${pfile} ]]; then
              print -P "%BInstalling from ${pfile}%b"
              if [[ ${pfile} == setup.py ]]; then
                python -m pip install -e . || return 1
              else
                python -m pip install -r ${pfile} || return 1
              fi
            fi
          done
    fi
    ;;
  ls)
    if [[ -e ${pvenv_dir} ]]; then
      print -R ${pvenv_dir}
    fi
    ;;
  rm)
    if [[ -e ${pvenv_dir} ]]; then
      print -R "Removing venv under ${pvenv_dir}"
      command rm -rf ${pvenv_dir}
    else
      print -u2 -PR "%F{red}${0}: No venv found for current directory.%f"
      return 1
    fi
    ;;
  *)
    print -u2 -PR "%F{red}${0}: Unknown action ${1}%f"$'\n\n'${pusage}
    return 2
    ;;
esac
